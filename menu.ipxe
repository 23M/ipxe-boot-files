#!ipxe

# Some menu defaults
set menu-timeout 12000
set submenu-timeout ${menu-timeout}
set menu-default exit

###################### MAIN MENU ####################################

:start
menu 23M PXE Boot for Install and Maintenance
item --gap --             ------------------------- Installers --------------------------
item --key a lnxauto      Linux choices (automated installation)
item --key m lnxman       Linux choices (manual installation)
item freebsd13            FreeBSD 13
item --gap --             ------------------------- LiveCD ------------------------------
item grml                 GRML (latest)
item --gap --             ------------------------- Advanced options --------------------
item --key c config       Configure settings
item shell                Drop to iPXE shell
item reload               Reload boot menu
item reboot               Reboot computer
item
item --key x exit         Exit and continue BIOS boot
choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
set menu-timeout 0
goto ${selected}

:lnxauto
menu 23M PXE Boot > Linux (automated)
item centos7ks             CentOS 7 (Default 23M)
item --key c centos8ks     CentOS 8 (Default 23M)
item debian10ps            Debian 10 (Default 23M)
item --key d debian11ps    Debian 11 (Default 23M)
item ubuntu1804ps          Ubuntu 18.04 (LTS) (Default 23M)
item --key u ubuntu2004ps  Ubuntu 20.04 (LTS) (Default 23M)
item
item start                 Go back
choose selected || goto cancel
goto ${selected}

:lnxman
menu 23M PXE Boot > Linux (manual)
item --key a almalinux     AlmaLinux
item centos7               CentOS 7
item --key c centos8       CentOS 8
item debian10              Debian 10
item --key d debian11      Debian 11
item ubuntu1804            Ubuntu 18.04 (LTS)
item --key u ubuntu2004    Ubuntu 20.04 (LTS)
item
item start                 Go back
choose selected || goto cancel
goto ${selected}

:cancel
echo You cancelled the menu, dropping you to a shell

:reload
chain -ar menu.ipxe

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

:error
echo An error occurred, press any key to return to menu ...
prompt
goto start

:almalinux
chain os/almalinux.ipxe || goto error

:centos7
chain os/centos7.ipxe || goto error

:centos7ks
chain os/centos7-ks.ipxe || goto error

:centos8
chain os/centos8.ipxe || goto error

:centos8ks
chain os/centos8-ks.ipxe || goto error

:debian10
chain os/debian10.ipxe || goto error

:debian11
chain os/debian11.ipxe || goto error

:debian11ps
chain os/debian11-ps.ipxe || goto error

:freebsd13
chain os/freebsd13.ipxe || goto error

:grml
chain os/grml.ipxe || goto error

:ubuntu1804
chain os/ubuntu1804.ipxe || goto error

:ubuntu1804ps
chain os/ubuntu1804-ps.ipxe || goto error

:ubuntu2004
chain os/ubuntu2004.ipxe || goto error

:ubuntu2004ps
chain os/ubuntu2004-ps.ipxe || goto error